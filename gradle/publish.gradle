apply plugin: 'maven-publish'

def isAndroid = project.getPlugins().hasPlugin('com.android.application') || project.getPlugins().hasPlugin('com.android.library')
if (isAndroid) {
    task generateAndroidSourcesJar(type: Jar) {
        from android.sourceSets.main.java.srcDirs
        classifier 'sources'
    }
} else {
    task generateSourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier 'sources'
    }
}

afterEvaluate {
    publishing {
        publications {
            if (isAndroid) {
                releaseAndroid(MavenPublication) {
                    from components.release

                    groupId = 'net.evilmouth'
                    artifactId = project.name
                    version = '1.0.0-SNAPSHOT'

                    artifact generateAndroidSourcesJar
                }
            } else {
                releaseJava(MavenPublication) {
                    from components.java

                    groupId = 'net.evilmouth'
                    artifactId = project.name
                    version = '1.0.0-SNAPSHOT'

                    artifact generateSourcesJar
                }
            }
        }
        repositories {
            maven {
                url "$rootDir/repo/"
            }
        }
    }
}
